//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2023 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

Процедура ИсполняемыеСценарии() Экспорт
	
	ЮТТесты
		.ДобавитьТест("ТаблицаЗначенийИзМассиваСтруктур");
		
КонецПроцедуры

#Область События

Процедура ПередВсемиТестами() Экспорт
	
КонецПроцедуры

Процедура ПередКаждымТестом() Экспорт
	
КонецПроцедуры

Процедура ПослеКаждогоТеста() Экспорт
	
КонецПроцедуры

Процедура ПослеВсехТестов() Экспорт
	
КонецПроцедуры

#КонецОбласти

Процедура ТаблицаЗначенийИзМассиваСтруктур() Экспорт
	
	// Подготовка тестового окружения
	
	ТаблицаMarkDown = 
	"| Товар   | Товар_tchk_Поставщик  | Товар_tchk_Вид | Цена | Количество | Сумма |
	||---------|-----------------------|----------------|------|------------|-------|
	|| Товар 1 | Пустой поставщик      | Товар          | 100  | 1          | 100   |
	|| Товар 2 | Пустой поставщик      | Товар          | 200  | 2          | 400   |
	|| Услуга  | Поставщик             | Услуга         | 300  | 1          | 300   |
	|";
	
	МассивСтруктур = ЮТест.Данные().ТаблицаMarkDown(ТаблицаMarkDown);
	
	ОписанияТипов = Новый Соответствие;
	ОписанияТипов.Вставить("Товар", Новый ОписаниеТипов("СправочникСсылка.Товары"));
	ОписанияТипов.Вставить("Цена", Новый ОписаниеТипов("Число"));
	ОписанияТипов.Вставить("Количество", Новый ОписаниеТипов("Число"));
	ОписанияТипов.Вставить("Сумма", Новый ОписаниеТипов("Число"));
		
	ПустойПоставщик = Справочники.Контрагенты.ПустаяСсылка();
	
	Параметры = Новый Соответствие;
	Параметры.Вставить("Пустой поставщик", ПустойПоставщик);
	
	КэшЗначений = Новый Соответствие;
		
	КоличествоСтрок = МассивСтруктур.Количество();
			
	// Вызов тестируемого сценария
	
	ТаблицаЗначений = ЮТТестовыеДанные_ТаблицыЗначений.ТаблицаЗначенийИзМассиваСтруктур(
		МассивСтруктур, 
		ОписанияТипов, 
		КэшЗначений, 
		Параметры
	);
	
	// Проверка поведения и результатов
	
	Ютест.ОжидаетЧто(ТаблицаЗначений)
		.ИмеетТип("ТаблицаЗначений")
		.ИмеетДлину(КоличествоСтрок)
		.КаждыйЭлементСодержитСвойство("Товар")
		.КаждыйЭлементСодержитСвойство("Цена")
		.КаждыйЭлементСодержитСвойство("Количество")
		.КаждыйЭлементСодержитСвойство("Сумма")
		.Свойство("[0].Товар.Наименование").Равно("Товар 1")
		.Свойство("[0].Товар.Вид").Равно(Перечисления.ВидыТоваров.Товар)
		.Свойство("[0].Товар.Поставщик").Равно(ПустойПоставщик)
		.Свойство("[0].Количество").Равно(1)
		.Свойство("[0].Цена").Равно(100)
		.Свойство("[0].Сумма").Равно(100)
		.Свойство("[1].Товар.Наименование").Равно("Товар 2")
		.Свойство("[1].Товар.Вид").Равно(Перечисления.ВидыТоваров.Товар)
		.Свойство("[1].Товар.Поставщик").Равно(ПустойПоставщик)
		.Свойство("[1].Количество").Равно(2)
		.Свойство("[1].Цена").Равно(200)
		.Свойство("[1].Сумма").Равно(400)
		.Свойство("[2].Товар.Наименование").Равно("Услуга")
		.Свойство("[2].Товар.Вид").Равно(Перечисления.ВидыТоваров.Услуга)
		.Свойство("[2].Товар.Поставщик").Заполнено()
		.Свойство("[2].Количество").Равно(1)
		.Свойство("[2].Цена").Равно(300)
		.Свойство("[2].Сумма").Равно(300);
		
	Ютест.ОжидаетЧто(ТаблицаЗначений.Колонки.Количество()).Равно(ОписанияТипов.Количество());		
	
	Ютест.ОжидаетЧто(КэшЗначений)
		.ИмеетДлину(3)
		.Свойство(Справочники.Товары).ИмеетДлину(3)
		.Свойство(Справочники.Контрагенты).ИмеетДлину(1)
		.Свойство(Перечисления.ВидыТоваров).ИмеетДлину(2);
		
КонецПроцедуры

#КонецОбласти
