//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2023 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

Процедура ИсполняемыеСценарии() Экспорт
	
	ЮТТесты
		.ВТранзакции()
		.УдалениеТестовыхДанных()
		.ДобавитьТест("ТипизированнаяТаблицаЗначений");
		
КонецПроцедуры

#Область События

Процедура ПередВсемиТестами() Экспорт
	
КонецПроцедуры

Процедура ПередКаждымТестом() Экспорт
	
КонецПроцедуры

Процедура ПослеКаждогоТеста() Экспорт
	
КонецПроцедуры

Процедура ПослеВсехТестов() Экспорт
	
КонецПроцедуры

#КонецОбласти

Процедура ТипизированнаяТаблицаЗначений() Экспорт
	
	// Подготовка тестового окружения
	
	ТабличныйДокумент = ПолучитьОбщийМакет("ЮТ_МакетТестовыхДанных");
	
	ИсходныеДанные = ТабличныйДокумент.ПолучитьОбласть(2, 1, 5, 11);
	МассивСтруктур = ЮТест.Данные().СтруктурыТабличногоДокумента(ИсходныеДанные);
	
	ОписанияТипов = Новый Соответствие;
	ОписанияТипов.Вставить("Период", Новый ОписаниеТипов("Дата"));
	ОписанияТипов.Вставить("Активность", Новый ОписаниеТипов("Булево"));
	ОписанияТипов.Вставить("Товар", Новый ОписаниеТипов("СправочникСсылка.Товары"));
	ОписанияТипов.Вставить("Цена", Новый ОписаниеТипов("Число"));
	ОписанияТипов.Вставить("Количество", Новый ОписаниеТипов("Число"));
	ОписанияТипов.Вставить("Сумма", Новый ОписаниеТипов("Число"));
	
	КэшЗначений = Новый Соответствие;
		
	КоличествоСтрок = МассивСтруктур.Количество();
	ОжидаемыйПериод = Дата(2023, 01, 01);
			
	// Вызов тестируемого сценария
	
	ТаблицаЗначений = ЮТТестовыеДанные_ТаблицыЗначений.ТипизированнаяТаблицаЗначений(
		МассивСтруктур, 
		ОписанияТипов, 
		КэшЗначений
	);
	
	// Проверка поведения и результатов
	
	Ютест.ОжидаетЧто(ТаблицаЗначений)
		.ИмеетТип("ТаблицаЗначений")
		.ИмеетДлину(КоличествоСтрок)
		.КаждыйЭлементСодержитСвойство("Период")
		.КаждыйЭлементСодержитСвойство("Активность")
		.КаждыйЭлементСодержитСвойство("Товар")
		.КаждыйЭлементСодержитСвойство("Цена")
		.КаждыйЭлементСодержитСвойство("Количество")
		.КаждыйЭлементСодержитСвойство("Сумма")
		.Свойство("[0].Период").Равно(ОжидаемыйПериод)
		.Свойство("[0].Активность").Равно(Истина)
		.Свойство("[0].Товар.Наименование").Равно("Товар 1")
		.Свойство("[0].Товар.Вид").Равно(Перечисления.ВидыТоваров.Товар)
		.Свойство("[0].Товар.Поставщик").Заполнено()
		.Свойство("[0].Количество").Равно(1)
		.Свойство("[0].Цена").Равно(100.55)
		.Свойство("[0].Сумма").Равно(100.55)
		.Свойство("[1].Период").Равно(ОжидаемыйПериод)
		.Свойство("[1].Активность").Равно(Истина)
		.Свойство("[1].Товар.Наименование").Равно("Товар 2")
		.Свойство("[1].Товар.Вид").Равно(Перечисления.ВидыТоваров.Товар)
		.Свойство("[1].Товар.Поставщик").Заполнено()
		.Свойство("[1].Количество").Равно(1)
		.Свойство("[1].Цена").Равно(1500.2)
		.Свойство("[1].Сумма").Равно(1500.2)
		.Свойство("[2].Период").Равно(ОжидаемыйПериод)
		.Свойство("[2].Активность").Равно(Ложь)
		.Свойство("[2].Товар.Наименование").Равно("Услуга")
		.Свойство("[2].Товар.Вид").Равно(Перечисления.ВидыТоваров.Услуга)
		.Свойство("[2].Товар.Поставщик").НеЗаполнено()
		.Свойство("[2].Количество").Равно(1)
		.Свойство("[2].Цена").Равно(1000000)
		.Свойство("[2].Сумма").Равно(1000000);
		
	Ютест.ОжидаетЧто(ТаблицаЗначений.Колонки.Количество()).Равно(ОписанияТипов.Количество());		
	
	Ютест.ОжидаетЧто(КэшЗначений)
		.ИмеетДлину(3)
		.Свойство(Справочники.Товары).ИмеетДлину(3)
		.Свойство(Справочники.Контрагенты).ИмеетДлину(2)
		.Свойство(Перечисления.ВидыТоваров).ИмеетДлину(2);
		
КонецПроцедуры

#КонецОбласти
