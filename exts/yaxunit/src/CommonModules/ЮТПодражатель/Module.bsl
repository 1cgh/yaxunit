//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2023 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

// @strict-types


#Область ПрограммныйИнтерфейс

// Локализация.
//
// Возвращаемое значение:
//  Строка - Возвращает текущую установленную локализацию
Функция Локализация() Экспорт
	Возврат Контекст().Локализация;
КонецФункции

// Устанавливает локализацию подражателя
// В дальнейшем используется для получения списка словарей с указанной локализацией
// Параметры:
//  КодЯзыка - Строка - Код языка
Процедура УстановитьЛокализацию(КодЯзыка) Экспорт
	Контекст().Локализация = КодЯзыка;
	ОбновитьПовторноИспользуемыеЗначения();
КонецПроцедуры

// Получает список из словаря с учетом установленной локализации
//
// Параметры:
//  ИмяРеализации - Строка - Имя реализации
//  ИмяСловаря - Строка - Имя словаря
//
// Возвращаемое значение:
//	ФиксированныйМассив из Строка
Функция Словарь(ИмяРеализации, ИмяСловаря) Экспорт
	Возврат ЮТПодражательПовтИсп.Словарь(ИмяРеализации, ИмяСловаря);
КонецФункции

#Область Реализации

Функция Люди() Экспорт
	Возврат ЮТПодражатель_Реализация_Люди;
КонецФункции

Функция Компании() Экспорт
	Возврат ЮТПодражатель_Реализация_Компании;
КонецФункции

Функция Банки() Экспорт
	Возврат ЮТПодражатель_Реализация_Банки;
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс
// Контекст.
//
// Возвращаемое значение:
//  см. НовыйКонтекст
Функция Контекст() Экспорт

	//@skip-check constructor-function-return-section
	Возврат ЮТКонтекст.ЗначениеКонтекста(КлючКонтекста());

КонецФункции

// Инициализирует конструктор предиката
//
// Возвращаемое значение:
//  ОбщийМодуль - Этот модуль для замыкания
Функция Инициализировать() Экспорт

	ЮТКонтекст.УстановитьЗначениеКонтекста(КлючКонтекста(), НовыйКонтекст());
	Возврат ЮТПодражатель;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция КлючКонтекста()

	Возврат "Подражатель";

КонецФункции

// Новый контекст.
//
// Возвращаемое значение:
//  Структура - Новый контекст:
// * Локализация - Строка - Установленная локализация
Функция НовыйКонтекст()

	Описание = Новый Структура;

	#Если Клиент Тогда
		Описание.Вставить("Локализация", ТекущийЯзык());
	#Иначе
		Описание.Вставить("Локализация", ТекущийЯзык().КодЯзыка);
	#КонецЕсли

	Возврат Описание;

КонецФункции

#КонецОбласти
