//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2023 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс
	
// Возвращает таблицу значений, заполненную из массива структур
//	Параметры:
//		Данные - Массив из Структура - Значения для преобразования в таблицу
//		ОписанияТипов - Соответствие из Строка - Соответствие имен колонок таблицы к типам значений
//      КэшЗначений - Соответствие из Произвольный - Соответствие для хранения создаваемых значений
//      ЗаменяемыеЗначения - Соответствие из Строка - Значения, использующиеся для замены
//      Параметры - см. ЮТТестовыеДанные_ТаблицыЗначений.ПараметрыЗаполненияТаблицыЗначений
//	Возвращаемое значение:
//      ТаблицаЗначений                  
Функция ТипизированнаяТаблицаЗначений(Данные, ОписанияТипов, КэшЗначений = Неопределено, 
	ЗаменяемыеЗначения = Неопределено, Параметры = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Данные) Тогда
		Возврат Новый ТаблицаЗначений;
	КонецЕсли;
	
	ИменаКолонок = ЮТОбщий.ВыгрузитьЗначения(Данные[0], "Ключ");
		
	НедостающиеКолонки = НедостающиеКолонкиДанных(ИменаКолонок, ОписанияТипов);
	Если ЗначениеЗаполнено(НедостающиеКолонки) Тогда
		ТекстИсключения = "Отсутствуют данные для ожидаемых колонок: " + СтрСоединить(НедостающиеКолонки, ",");
		Вызватьисключение ТекстИсключения;	
	КонецЕсли;
			
	Если ЗаменяемыеЗначения = Неопределено Тогда
		ЗаменяемыеЗначения = Новый Соответствие;
	КонецЕсли;

	Если Параметры = Неопределено Тогда
		Параметры = ПараметрыЗаполненияТаблицыЗначений();	
	КонецЕсли;
	
	ПараметрыСозданияОбъектовМетаданных = Параметры.СозданиеОбъектовМетаданных;
			
	СведенияОбъектовМетаданных = СведенияОбъектовМетаданныхИзОписанийТипов(ИменаКолонок, ОписанияТипов);
	СведенияОРеквизитахОбъектов = СведенияРеквизитовОбъектовМетаданных(ИменаКолонок, СведенияОбъектовМетаданных);
	ТаблицаЗначений = ИнициализированнаяТаблицаЗначений(ИменаКолонок, ОписанияТипов);
	
	ИнициализироватьКэшЗначенийОбъектовМетаданных(КэшЗначений, ИменаКолонок, СведенияОбъектовМетаданных); 
	
	Для Каждого Источник Из Данные Цикл
		
		НоваяСтрока = ТаблицаЗначений.Добавить();
		
		Для Каждого Колонка Из ТаблицаЗначений.Колонки Цикл
			
			ИмяКолонки = Колонка.Имя;
			ОписаниеТипа = Колонка.ТипЗначения;
			
			ЗначениеИсточника = Источник[ИмяКолонки];
			
			Если Не ЗначениеЗаполнено(ЗначениеИсточника) Тогда
				Продолжить;
			КонецЕсли;   
		
			СведенияОбъекта = СведенияОбъектовМетаданных.Получить(ИмяКолонки);
			ЗаменяемоеЗначение = ЗаменяемыеЗначения.Получить(ЗначениеИсточника);
			
			Если ЗаменяемоеЗначение <> Неопределено Тогда
				
				ЗначениеТаблицы = ЗаменяемоеЗначение;
				
			ИначеЕсли СведенияОбъекта <> Неопределено Тогда
				
				Менеджер = СведенияОбъекта.Менеджер;
				ОписаниеОбъектаМетаданных = СведенияОбъекта.ОписаниеОбъектаМетаданных;
				
				ЗначениеТаблицы = КэшЗначений[Менеджер].Получить(ЗначениеИсточника);
				
				Если ЗначениеТаблицы = Неопределено Тогда
					
					ЗначенияРеквизитовОбъекта = ПолучитьЗначенияРеквизитовПоУмолчанию(
						ОписаниеОбъектаМетаданных, 
						ЗначениеИсточника
					);
					
					СведенияРеквизитовОбъекта = СведенияОРеквизитахОбъектов.Получить(ИмяКолонки);
					Если СведенияРеквизитовОбъекта <> Неопределено Тогда	
						
						ДополнитьЗначенияРеквизитовОбъекта(
							ЗначенияРеквизитовОбъекта, 
							СведенияРеквизитовОбъекта, 
							Источник, 
							КэшЗначений,
							ЗаменяемыеЗначения,
							ПараметрыСозданияОбъектовМетаданных
						);
						
					КонецЕсли;
					
					ЗначениеТаблицы = ЗначениеОбъектаМетаданных(
						ОписаниеОбъектаМетаданных, 
						Менеджер, 
						ЗначенияРеквизитовОбъекта,
						ПараметрыСозданияОбъектовМетаданных
					);
					
					КэшЗначений[Менеджер].Вставить(ЗначениеИсточника, ЗначениеТаблицы);
					
				КонецЕсли;          
				
			Иначе
				
				ЗначениеТаблицы = ПривестиЗначение(ОписаниеТипа, ЗначениеИсточника);
  
			КонецЕсли;   
			
			НоваяСтрока[ИмяКолонки] = ЗначениеТаблицы;
			
		КонецЦикла;  
		
	КонецЦикла;  
	
	Возврат ТаблицаЗначений;
	
КонецФункции

// Возвращает фиксированную структуру параметров для заполнения таблицы значений
//	Параметры:
//		СозданиеОбъектовМетаданных - Структура:
//			* ФикцияОбязательныхПолей - Булево
//			* ОбменДаннымиЗагрузка - Булево
//	Возвращаемое значение:
//		ФиксированнаяСтруктура:
//			* СозданиеОбъектовМетаданных - ФиксированнаяСтруктура:
//				* ФикцияОбязательныхПолей - Булево,
//				* ОбменДаннымиЗагрузка - Булево		
Функция ПараметрыЗаполненияТаблицыЗначений(СозданиеОбъектовМетаданных = Неопределено) Экспорт
	
	ФикцияОбязательныхПолей = Ложь;
	ОбменДаннымиЗагрузка = Истина;
	
	Если ТипЗнч(СозданиеОбъектовМетаданных) = Тип("Структура") Тогда
		ЮТОбщий.ЗначениеСтруктуры(СозданиеОбъектовМетаданных, "ФикцияОбязательныхПолей", ФикцияОбязательныхПолей);
		ЮТОбщий.ЗначениеСтруктуры(СозданиеОбъектовМетаданных, "ОбменДаннымиЗагрузка", ОбменДаннымиЗагрузка);		
	КонецЕсли;		
	
	СозданиеОбъектовМетаданных = Новый Структура;
	СозданиеОбъектовМетаданных.Вставить("ФикцияОбязательныхПолей", ФикцияОбязательныхПолей);
	СозданиеОбъектовМетаданных.Вставить("ОбменДаннымиЗагрузка", ОбменДаннымиЗагрузка);
	
	Параметры = Новый Структура;
	Параметры.Вставить("СозданиеОбъектовМетаданных", СозданиеОбъектовМетаданных);
	
	Возврат Новый ФиксированнаяСтруктура(Параметры);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НедостающиеКолонкиДанных(ИменаКолонок, ОписанияТипов)
	
	НедостающиеКолонки = Новый Массив;
	ОжидаемыеКолонки = ЮТОбщий.ВыгрузитьЗначения(ОписанияТипов, "Ключ");
	Для Каждого ИмяКолонки Из ОжидаемыеКолонки Цикл
		Если ИменаКолонок.Найти(ИмяКолонки) = Неопределено Тогда
			НедостающиеКолонки.Добавить(ИмяКолонки);	
		КонецЕсли;			
	КонецЦикла;	
	
	Возврат НедостающиеКолонки;
	
КонецФункции
			
Функция СведенияОбъектовМетаданныхИзОписанийТипов(ИменаКолонок, ОписанияТипов)
	
	МенеджерыЗначений = Новый Соответствие;
	
	Для Каждого ИмяКолонки Из ИменаКолонок Цикл
		
		ОписаниеТипа = ОписанияТипов.Получить(ИмяКолонки);
		ОписаниеОбъектаМетаданных = ОписаниеОбъектМетаданныхПоОписаниюТипа(ОписаниеТипа);
		
		Если ОписаниеОбъектаМетаданных = Неопределено Тогда
			Продолжить;
		КонецЕсли;
			
		Менеджер = МенеджерОбъектаИзОписанияОбъектаМетаданных(ОписаниеОбъектаМетаданных);
		
		Сведения = Новый Структура;
		Сведения.Вставить("Менеджер", 	Менеджер);
		Сведения.Вставить("ОписаниеОбъектаМетаданных", 	ОписаниеОбъектаМетаданных);
		
		МенеджерыЗначений.Вставить(ИмяКолонки, Сведения);		
		
	КонецЦикла;		
	
	Возврат МенеджерыЗначений;
	
КонецФункции
	
Функция ОписаниеОбъектМетаданныхПоОписаниюТипа(ОписаниеТипа)
	
	ОписаниеОбъектаМетаданных = Неопределено;
	
	Тип = ТипИзОписанияТипа(ОписаниеТипа);
	
	Если Тип = Неопределено Тогда
		Возврат ОписаниеОбъектаМетаданных;
	КонецЕсли;
	
	ОбъектМетаданных = Метаданные.НайтиПоТипу(Тип);
	
	Если ОбъектМетаданных = Неопределено Тогда
		Возврат ОписаниеОбъектаМетаданных;
	КонецЕсли;	
	
	Если Метаданные.Перечисления.Содержит(ОбъектМетаданных) Тогда
		ОписаниеОбъектаМетаданных = Новый Структура;
		ОписаниеОбъектаМетаданных.Вставить("Имя", ОбъектМетаданных.Имя);
	Иначе	
		ОписаниеОбъектаМетаданных = ЮтМетаданные.ОписаниеОбъектМетаданных(ОбъектМетаданных.ПолноеИмя());
	КонецЕсли;
	
	Возврат ОписаниеОбъектаМетаданных;
		
КонецФункции

Функция ТипИзОписанияТипа(ОписаниеТипа)
	
	Если ОписаниеТипа = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
		
	Если ТипЗнч(ОписаниеТипа) <> Тип("ОписаниеТипов") Или Не ЗначениеЗаполнено(ОписаниеТипа.Типы()) Тогда
		Вызватьисключение ЮТОбщий.НеподдерживаемыйПараметрМетода(
			"ЮТТестовыеДанные_ТаблицыЗначений.ТипИзОписанияТипа", 
			ОписаниеТипа
		);
	КонецЕсли;
				
	Возврат ОписаниеТипа.Типы()[0];
		
КонецФункции
			
Функция МенеджерОбъектаИзОписанияОбъектаМетаданных(ОписаниеОбъектаМетаданных)
	
	Если Метаданные.Перечисления.Найти(ОписаниеОбъектаМетаданных.Имя) <> Неопределено Тогда
		
		Менеджер = Новый ("ПеречислениеМенеджер." + ОписаниеОбъектаМетаданных.Имя);
		
	Иначе
		
		ОписаниеТипа = ОписаниеОбъектаМетаданных.ОписаниеТипа;
		
		ИмяТипаМенеджера = СтрШаблон("%1Менеджер.%2", ОписаниеТипа.Имя, ОписаниеОбъектаМетаданных.Имя);
		Менеджер = Новый (ИмяТипаМенеджера);
		
	КонецЕсли;
	
	Возврат Менеджер;
	
КонецФункции
	
Функция СведенияРеквизитовОбъектовМетаданных(ИменаКолонок, СведенияОбъектовМетаданных)
	
	ПодстрокиДляЗамены = ЮТТестовыеДанныеСлужебный.ПодстрокиДляЗаменыВИменахСвойств();
	ПодстрокаЗаменыТочки = ПодстрокиДляЗамены.Получить(".");
	
	СведенияРеквизитовОбъекта = Новый Соответствие;
	
	Для Каждого ИмяКолонкиОбъекта Из ИменаКолонок Цикл
		
		СведенияОбъектаМетаданных = СведенияОбъектовМетаданных.Получить(ИмяКолонкиОбъекта);
		
		Если СведенияОбъектаМетаданных = Неопределено Тогда
			Продолжить;
		КонецЕсли;
			
		ОписаниеОбъектаМетаданных = СведенияОбъектаМетаданных.ОписаниеОбъектаМетаданных;
	
		МассивСведений = Новый Массив;  
		
		Для Каждого ИмяКолонки Из ИменаКолонок Цикл
			
			Если СтрНайти(ИмяКолонки, ИмяКолонкиОбъекта) = 0 Или СтрНайти(ИмяКолонки, ПодстрокаЗаменыТочки) = 0 Тогда
				Продолжить;
			КонецЕсли;
				
			Массив = СтрРазделить(ИмяКолонки, ПодстрокаЗаменыТочки, Ложь); 
			
			Если Массив.Количество() <> 2 Тогда
				Продолжить;
			Иначе
				РеквизитОбъекта = Неопределено;
            	ОписаниеОбъектаМетаданных.Реквизиты.Свойство(Массив[1], РеквизитОбъекта);
				Если РеквизитОбъекта <> Неопределено Тогда
					Сведения = Новый Структура;
					Сведения.Вставить("Идентификатор", ИмяКолонки);
					Сведения.Вставить("ИмяРеквизита", РеквизитОбъекта.Имя);
					Сведения.Вставить("ОписаниеТипа", РеквизитОбъекта.Тип);
					МассивСведений.Добавить(Сведения);  
				КонецЕсли;	
			КонецЕсли;  
			
		КонецЦикла;
		
		Если ЗначениеЗаполнено(МассивСведений) Тогда
			СведенияРеквизитовОбъекта.Вставить(ИмяКолонкиОбъекта, МассивСведений);   
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СведенияРеквизитовОбъекта;
	 	
КонецФункции
	
Функция ИнициализированнаяТаблицаЗначений(ИменаКолонок, ОписанияТипов)
	
	ТаблицаЗначений = Новый ТаблицаЗначений;
	
	Для Каждого ИмяКолонки Из ИменаКолонок Цикл
		
		ОписаниеТипаКолонки = ОписанияТипов.Получить(ИмяКолонки);
		
		Если ОписаниеТипаКолонки = Неопределено Тогда
			Продолжить;
		КонецЕсли;
			
		ТаблицаЗначений.Колонки.Добавить(ИмяКолонки, ОписаниеТипаКолонки);
		
	КонецЦикла;
	 
	Возврат ТаблицаЗначений;
	
КонецФункции
	
Процедура ИнициализироватьКэшЗначенийОбъектовМетаданных(КэшЗначений, ИменаКолонок, СведенияОбъектовМетаданных)
	
	Если Не ЗначениеЗаполнено(КэшЗначений) Тогда
		КэшЗначений = Новый Соответствие;
	КонецЕсли;
		
	Для Каждого ИмяКолонки Из ИменаКолонок Цикл
		
		СведенияОбъекта = СведенияОбъектовМетаданных.Получить(ИмяКолонки);
		Если СведенияОбъекта = Неопределено Тогда
			Продолжить;
		КонецЕсли;
			
		Менеджер = СведенияОбъекта.Менеджер;
		Если КэшЗначений.Получить(Менеджер) = Неопределено Тогда
			КэшЗначений.Вставить(Менеджер, Новый Соответствие);
		КонецЕсли;
			
	КонецЦикла; 
	
КонецПроцедуры

Функция ПолучитьЗначенияРеквизитовПоУмолчанию(ОписаниеОбъектаМетаданных, Значение)
	
	ЗначенияРеквизитовПоУмолчанию = Новый Структура;
	
	Если Метаданные.Перечисления.Найти(ОписаниеОбъектаМетаданных.Имя) <> Неопределено Тогда
		ЗначенияРеквизитовПоУмолчанию.Вставить("Значение", Значение);
	ИначеЕсли ОписаниеОбъектаМетаданных.ОписаниеТипа.Имя = "Справочник" Тогда
		ИмяРеквизита = "Наименование";
		Если ОписаниеОбъектаМетаданных.Реквизиты.Свойство(ИмяРеквизита) = Неопределено Тогда
			ИмяРеквизита = "Код";			
		КонецЕсли;	
		ЗначенияРеквизитовПоУмолчанию.Вставить(ИмяРеквизита, Значение);
	КонецЕсли;
	
	Возврат ЗначенияРеквизитовПоУмолчанию;
	
КонецФункции

Функция ДополнитьЗначенияРеквизитовОбъекта(Результат, СведенияРеквизитов, Источник, КешЗначений, ЗаменяемыеЗначения, 
	ПараметрыСозданияОбъектовМетаданных)
		
	Для Каждого Сведения Из СведенияРеквизитов Цикл
		
		ЗначениеИсточника = Источник[Сведения.Идентификатор];
		
		Если ЗначениеЗаполнено(ЗначениеИсточника) Тогда
			
			ЗаменяемоеЗначение = ЗаменяемыеЗначения.Получить(ЗначениеИсточника);
			ОписаниеОбъектаМетаданных = ОписаниеОбъектМетаданныхПоОписаниюТипа(Сведения.ОписаниеТипа);	
			
			Если ЗаменяемоеЗначение <> Неопределено Тогда
				
				ЗначениеРеквизита = ЗаменяемоеЗначение;	
				
			ИначеЕсли ОписаниеОбъектаМетаданных = Неопределено Тогда
				
				ЗначениеРеквизита = Сведения.ОписаниеТипа.ПривестиЗначение(ЗначениеИсточника);		
					
			Иначе	
				
				Менеджер = МенеджерОбъектаИзОписанияОбъектаМетаданных(ОписаниеОбъектаМетаданных); 
				
				ЗначенияМенеджера = КешЗначений[Менеджер];
				Если ЗначенияМенеджера = Неопределено Тогда
					ЗначенияМенеджера = Новый Соответствие;
					КешЗначений.Вставить(Менеджер, ЗначенияМенеджера);
				КонецЕсли;	
				
				ЗначениеРеквизита = ЗначенияМенеджера.Получить(ЗначениеИсточника);
				Если ЗначениеРеквизита = Неопределено Тогда
					
					ЗначенияРеквизитовОбъекта = ПолучитьЗначенияРеквизитовПоУмолчанию(
						ОписаниеОбъектаМетаданных, 
						ЗначениеИсточника
					);
					
					ЗначениеРеквизита = ЗначениеОбъектаМетаданных(
						ОписаниеОбъектаМетаданных, 
						Менеджер, 
						ЗначенияРеквизитовОбъекта,
						ПараметрыСозданияОбъектовМетаданных
					);
					
					КешЗначений[Менеджер].Вставить(ЗначениеИсточника, ЗначениеРеквизита);
						
				КонецЕсли;	
						
			КонецЕсли;	
			
		Иначе
			
			ЗначениеРеквизита = Неопределено;	
			
		КонецЕсли;
		
		Результат.Вставить(Сведения.ИмяРеквизита, ЗначениеРеквизита);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ЗначениеОбъектаМетаданных(ОписаниеОбъектаМетаданных, Менеджер, ЗначенияРеквизитов, Параметры)
	
	Если Метаданные.Перечисления.Найти(ОписаниеОбъектаМетаданных.Имя) <> Неопределено Тогда	
		
		Значение = Менеджер[ЗначенияРеквизитов.Значение];
		
    Иначе
		
		КонструкторОбъекта = ЮТест.Данные().КонструкторОбъекта(Менеджер); 
		Для Каждого ДанныеЗначения Из ЗначенияРеквизитов Цикл
			КонструкторОбъекта.Установить(ДанныеЗначения.Ключ, ДанныеЗначения.Значение); 
		КонецЦикла;    
		
		Если Параметры.ФикцияОбязательныхПолей Тогда
			КонструкторОбъекта.ФикцияОбязательныхПолей();	
		КонецЕсли;
			
		Значение = КонструкторОбъекта.Записать(, Параметры.ОбменДаннымиЗагрузка);
					
	КонецЕсли;
					
	Возврат Значение;
						
КонецФункции

Функция ПривестиЗначение(ОписаниеТипа, Значение)
	
	Если ОписаниеТипа.СодержитТип(Тип("Дата")) Тогда
		Результат = ПривестиЗначениеКДате(ОписаниеТипа, Значение);
	ИначеЕсли ОписаниеТипа.СодержитТип(Тип("Число")) Тогда
		Результат = ПривестиЗначениеКЧислу(ОписаниеТипа, Значение);	
	Иначе			
		Результат = ОписаниеТипа.ПривестиЗначение(Значение);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПривестиЗначениеКДате(ОписаниеТипа, Знач Значение)
	
	Результат = ОписаниеТипа.ПривестиЗначение(Значение);
	Если ЗначениеЗаполнено(Результат) Тогда
		Возврат Результат;
	КонецЕсли;
	
	МассивРазделителей = Новый Массив;
	МассивРазделителей.Добавить(".");
	МассивРазделителей.Добавить("/");
	МассивРазделителей.Добавить("-");
	
	СтрокаДаты = Неопределено;
	Для Каждого Разделитель Из МассивРазделителей Цикл
		
		Если ЗначениеЗаполнено(СтрокаДаты) Тогда
			Прервать;
		КонецЕсли;
			
		МассивЧастейДат = СтрРазделить(Значение, Разделитель);
		Если МассивЧастейДат.Количество() = 3 Тогда
			Если СтрДлина(МассивЧастейДат[2]) = 4 Тогда
				СтрокаДаты = МассивЧастейДат[2] + МассивЧастейДат[1] + МассивЧастейДат[0];
			ИначеЕсли СтрДлина(МассивЧастейДат[0]) = 4 Тогда
				СтрокаДаты = МассивЧастейДат[0] + МассивЧастейДат[1] + МассивЧастейДат[2];
			Иначе
				СтрокаДаты = "20" + МассивЧастейДат[2] + МассивЧастейДат[1] + МассивЧастейДат[0];			
			КонецЕсли;
		КонецЕсли;
			
	КонецЦикла;
					
	Результат = ОписаниеТипа.ПривестиЗначение(СтрокаДаты);
	
	Возврат Результат;
	
КонецФункции

Функция ПривестиЗначениеКЧислу(ОписаниеТипа, Знач Значение)
	
	Результат = ОписаниеТипа.ПривестиЗначение(Значение);
	Если ЗначениеЗаполнено(Результат) Тогда
		Возврат Результат;
	КонецЕсли;

	Значение = СтрЗаменить(Значение, " ", "");
	Результат = ОписаниеТипа.ПривестиЗначение(Значение);
	
	Возврат Результат;
	
КонецФункции
	
#КонецОбласти
