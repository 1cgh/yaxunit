//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2023 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область ПрограммныйИнтерфейс

// Возвращает случайное имя
//
// Параметры:
//  Пол - Строка - см. ЮТПодражатель_Реализация_Люди.ПолЧеловека
//
// Возвращаемое значение:
//	Строка
Функция Имя(Пол = Неопределено) Экспорт

	ИтоговыйСловарь = Новый Массив();

	Если Пол = ПолЧеловека().Мужской Или Пол = Неопределено Тогда
		ЮТОбщий.ДополнитьМассив(ИтоговыйСловарь, СловарьМужскиеИмена());
	КонецЕсли;

	Если Пол = ПолЧеловека().Женский Или Пол = Неопределено  Тогда
		ЮТОбщий.ДополнитьМассив(ИтоговыйСловарь, СловарьЖенскиеИмена());
	КонецЕсли;

	Возврат ИтоговыйСловарь.Получить(ЮТТестовыеДанные.СлучайноеЧисло(0, ИтоговыйСловарь.ВГраница()));

КонецФункции

// Возвращает случайную фамилию
//
// Параметры:
//  Пол - Строка - см. ЮТПодражатель_Реализация_Люди.ПолЧеловека
//
// Возвращаемое значение:
//	Строка
Функция Фамилия(Пол = Неопределено) Экспорт

	ИтоговыйСловарь = Новый Массив();

	Если Пол = ПолЧеловека().Мужской Или Пол = Неопределено Тогда
		ЮТОбщий.ДополнитьМассив(ИтоговыйСловарь, СловарьМужскиеФамилии());
	КонецЕсли;

	Если Пол = ПолЧеловека().Женский Или Пол = Неопределено Тогда
		ЮТОбщий.ДополнитьМассив(ИтоговыйСловарь, СловарьЖенскиеФамилии());
	КонецЕсли;

	Возврат ИтоговыйСловарь.Получить(ЮТТестовыеДанные.СлучайноеЧисло(0, ИтоговыйСловарь.ВГраница()));

КонецФункции

// Возвращает случайное отчество
//
// Параметры:
//  Пол - Строка - см. ЮТПодражатель_Реализация_Люди.ПолЧеловека
//
// Возвращаемое значение:
//	Строка
Функция Отчество(Пол = Неопределено) Экспорт

	ИтоговыйСловарь = Новый Массив();

	Если Пол = ПолЧеловека().Мужской Или Пол = Неопределено Тогда
		ЮТОбщий.ДополнитьМассив(ИтоговыйСловарь, СловарьМужскиеОтчества());
	КонецЕсли;

	Если Пол = ПолЧеловека().Женский Или Пол = Неопределено Тогда
		ЮТОбщий.ДополнитьМассив(ИтоговыйСловарь, СловарьЖенскиеОтчества());
	КонецЕсли;

	Возврат ИтоговыйСловарь.Получить(ЮТТестовыеДанные.СлучайноеЧисло(0, ИтоговыйСловарь.ВГраница()));

КонецФункции

// Возвращает случайное отчество
//
// Параметры:
//  Пол - Строка - см. ЮТПодражатель_Реализация_Люди.ПолЧеловека
//
// Возвращаемое значение:
//	Строка
Функция ФИО(Пол = Неопределено) Экспорт
	Возврат СтрШаблон(
		"%1 %2 %3",
		Фамилия(Пол),
		Имя(Пол),
		Отчество(Пол)
	);
КонецФункции

// Формирует случаный ИНН физического лица
//
// Возвращаемое значение:
// 	Строка
Функция ИНН()
	Возврат ЮТТестовыеДанные.Подражатель().Компании().ИНН(, Истина);
КонецФункции

#Область ФабрикаПеречислений

Функция ПолЧеловека() Экспорт

	Результат = Новый Структура();
	Результат.Вставить("Мужской", "Мужской");
	Результат.Вставить("Женский", "Женский");
	Возврат Новый ФиксированнаяСтруктура(Результат);

КонецФункции

#КонецОбласти


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИмяРеализации()
	Возврат "Люди";
КонецФункции

Функция СловарьЖенскиеИмена()
	Возврат ЮТПодражатель.Словарь(ИмяРеализации(), "ЖенскиеИмена");
КонецФункции
Функция СловарьЖенскиеФамилии()
	Возврат ЮТПодражатель.Словарь(ИмяРеализации(), "ЖенскиеФамилии");
КонецФункции
Функция СловарьЖенскиеОтчества()
	Возврат ЮТПодражатель.Словарь(ИмяРеализации(), "ЖенскиеОтчества");
КонецФункции

Функция СловарьМужскиеИмена()
	Возврат ЮТПодражатель.Словарь(ИмяРеализации(), "МужскиеИмена");
КонецФункции
Функция СловарьМужскиеФамилии()
	Возврат ЮТПодражатель.Словарь(ИмяРеализации(), "МужскиеФамилии");
КонецФункции
Функция СловарьМужскиеОтчества()
	Возврат ЮТПодражатель.Словарь(ИмяРеализации(), "МужскиеОтчества");
КонецФункции

#КонецОбласти
