//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2023 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область СлужебныйПрограммныйИнтерфейс

Функция ФайлКомпоненты(Знач ИмяМакета, Знач ОперационнаяСистема, Знач Архитектура) Экспорт
	
	Данные = ЮТОбщийВызовСервера.Макет(ИмяМакета);
	ЧтениеАрхива = Новый ЧтениеZipФайла();
	ЧтениеАрхива.Открыть(Данные.ОткрытьПотокДляЧтения());
	
	ИмяФайлаКомпоненты = ИмяФайлаКомпоненты(ЧтениеАрхива, ОперационнаяСистема, Архитектура);
	
	Если ИмяФайлаКомпоненты = Неопределено Тогда
		ВызватьИсключение "Компонента не поддреживает клиентское окружение";
	КонецЕсли;
	
	Данные = ДвоичныеДанныеЭлемента(ЧтениеАрхива, ИмяФайлаКомпоненты);
	
	Возврат Новый ФиксированнаяСтруктура("ИмяФайла, Данные", ИмяФайлаКомпоненты, Данные);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИмяФайлаКомпоненты(ЧтениеАрхива, ОперационнаяСистема, Архитектура)
	
	Данные = ДвоичныеДанныеЭлемента(ЧтениеАрхива, "MANIFEST.XML");
	
	Если Данные = Неопределено Тогда
		ВызватьИсключение "Архив компоненты не содержит манифеста";
	КонецЕсли;
	
	ЧтениеXML = Новый ЧтениеXML();
	ЧтениеXML.ОткрытьПоток(Данные.ОткрытьПотокДляЧтения());
	
	ИмяФайлаКомпоненты = Неопределено;
	
	Пока ЧтениеXML.Прочитать() Цикл
		
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента И СтрСравнить(ЧтениеXML.Имя, "component") = 0 Тогда
			
			ОперационнаяСистемаУзла = Неопределено;
			АрхитектураУзла = Неопределено;
			ПутьУзла = Неопределено;
			
			Для Инд = 0 По ЧтениеXML.КоличествоАтрибутов() - 1 Цикл
				
				ИмяАтрибута = ЧтениеXML.ЛокальноеИмяАтрибута(Инд);
				Если СтрСравнить(ИмяАтрибута, "os") = 0 Тогда
					ОперационнаяСистемаУзла = ЧтениеXML.ЗначениеАтрибута(Инд);
				ИначеЕсли СтрСравнить(ИмяАтрибута, "path") = 0 Тогда
					ПутьУзла = ЧтениеXML.ЗначениеАтрибута(Инд);
				ИначеЕсли СтрСравнить(ИмяАтрибута, "arch") = 0 Тогда
					АрхитектураУзла = ЧтениеXML.ЗначениеАтрибута(Инд);
				КонецЕсли;
				
			КонецЦикла;
			
			Если ОперационнаяСистема = ОперационнаяСистемаУзла И Архитектура = АрхитектураУзла Тогда
				ИмяФайлаКомпоненты = ПутьУзла;
				Прервать;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ЧтениеXML.Закрыть();
	
	Возврат ИмяФайлаКомпоненты;
	
КонецФункции

Функция ДвоичныеДанныеЭлемента(ЧтениеАрхива, ИмяФайла)
	
	ЭлементФайла = Неопределено;
	
	Для Каждого Элемент Из ЧтениеАрхива.Элементы Цикл
		Если СтрСравнить(Элемент.ПолноеИмя, ИмяФайла) = 0 Тогда
			ЭлементФайла = Элемент;
		КонецЕсли;
	КонецЦикла;
	
	Если ЭлементФайла = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
	ЧтениеАрхива.Извлечь(ЭлементФайла, ИмяВременногоФайла, РежимВосстановленияПутейФайловZIP.НеВосстанавливать);
	
	Данные = Новый ДвоичныеДанные(ЮТФайлы.ОбъединитьПути(ИмяВременногоФайла, ИмяФайла));
	УдалитьФайлы(ИмяВременногоФайла);
	
	Возврат Данные;
	
КонецФункции

#КонецОбласти
